on first join:
	set join message to "&7[&a+&7] %player% &8(&7%number of all offline players%&8)"
	add 1 to {analytics::firstJoins}
	add 1 to {analytics::joins}	

on join:
	set join message to "&7[&a+&7] %player%"
	add 1 to {analytics::joins}

on quit:
	set quit message to "&7[&c-&7] %player%"
	add 1 to {analytics::quits}

on chat:
	set {_uuid} to player's uuid
	set {_prestige} to {player::%{_uuid}%::prestige}
	set {_roomTag} to {rooms::%progressionToID({player::%{_uuid}%::progression})%::tag}
	set {_suffix} to {suffixes::%{player::%{_uuid}%::suffix}%}

	if player has permission "chat.stats":
		replace all "[eggs]" with "&e%formatNum({player::%player's uuid%::eggs})% Eggs&7" in message
		replace all "[tokens]" with "&e%{player::%player's uuid%::tokens}% Tokens&7" in message
		replace all "[prestige]" and "[prestiges]" with "&e%{player::%player's uuid%::prestige}% Prestiges&7" in message
		replace all "[multi]" and "[multiplier]" with "&ex%{player::%player's uuid%::multiplier}% Multiplier&7" in message
		replace all "[clicks]" with "&e%formatNum({player::%player's uuid%::clicks})% Clicks&7" in message

	if player has permission "staff":
		set chat format to "&7[&e✫%{_prestige}%&7] [&e%{_roomTag}%&7] &r%player's prefix% %player's name% %{_suffix}%&8» &f%formatted message%"
	else if player has permission "chat.colorchat":		
		set chat format to "&7[&e✫%{_prestige}%&7] [&e%{_roomTag}%&7] &r%player's prefix% %player's name% %{_suffix}%&8» &f%colored message%"
	else if player has permission "chat.whitechat":
		set chat format to "&7[&e✫%{_prestige}%&7][&e%{_roomTag}%&7] &r%player's prefix% %player's name% %{_suffix}%&8» &f%message%"
	else:
		set chat format to "&7[&e✫%{_prestige}%&7] [&e%{_roomTag}%&7] &r%player's prefix% %player's name% %{_suffix}%&8» &7%message%"

command /buy:
	trigger:
		send formatted "<link:https://eggclicker.craftingstore.net/>&aClick on this message to support the server!" to player

command /reloadsuffixes:
	permission: op 
	permission message: &cYou cannot use this command!
	trigger:
		reloadSuffixes()
		send "&aSuccessfully loaded &e%size of {suffixes::*}% &asuffixes!"

function reloadSuffixes():
	clear {suffixes::*}
	set {suffixes::none} to ""
	set {suffixes::chicken} to "&7[&cChicken&7] "
	set {suffixes::gamer} to "&7[&eGamer&7] " 
	set {suffixes::hacker} to "&7[&aHacker&7] "
	set {suffixes::swedish} to "&7[&2Swedish&7] "
	set {suffixes::drip} to "&7[&bDrip&7] "
	set {suffixes::pro} to "&7[&3Pro&7] "
	set {suffixes::noob} to "&7[&9Noob&7] "
	set {suffixes::nerd} to "&7[&5Nerd&7] "
	set {suffixes::fairy} to "&7[&dFairy&7] "

command /discord:
	trigger:
		send formatted "<link:https://discord.gg/TTZVZbXG7e>&bClick on this message to join the discord server!" to player
		play sound "block.note_block.chime" at volume 10

every 5 minutes:
	send action bar "&6&lEggClicker &7>> &eMake sure to join the &b/discord &eto get server news and updates!" to all players