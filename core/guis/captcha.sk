function loadCaptcha(p: player):
    set {_uuid} to uuid of {_p}
    set {player::%{_uuid}%::lastCaptcha} to now
    if {player::%{_uuid}%::lastLocation} is location of {_p}:
        set {player::%{_uuid}%::slot} to random integer between 0 and 53
        loadGUI({_p}, "captcha", 6, "Captcha")
        setGUISlot({_p}, "captcha", {player::%{_uuid}%::slot}, paper named "&a&lCLICK ME!")
        openGUI({_p}, "captcha")
    else:
        set {player::%{_uuid}%::lastLocation} to location of {_p}

on inventory click:
    if event-inventory = (metadata tag "captcha" of player):
        cancel event
        set {_uuid} to uuid of player
        if event-slot is {player::%{_uuid}%::slot}:
            close player's inventory
            delete {player::%{_uuid}%::slot}
            give player 1 of instaClick(250)
            send "&aYou completed the captcha and received an &eInsta-Click &aworth &e250&a!"
            play sound "block.note_block.bell" to player
            
on join:
    set {player::%player's uuid%::lastCaptcha} to now
    set {player::%player's uuid%::lastLocation} to location of player