command /reloadcollectoritems:
	permission: op
	permission message: &cYou do not have permission to use this command.
	trigger:
		loadCollectorItems()
		send "&aCollector items reloaded!" to player

function loadCollectorItems():
	clear {collectorItems::*}
	set {collectorItems::magicDust} to 1 of glowstone dust named "&dMagic Dust" with lore "&7&oExtracted by fairies. Found", "&7&oin large flowers.", "", and "&6&lQUEST ITEM"
	set {collectorItems::emptyScroll} to 1 of empty map named "&eEmpty Scroll" with lore "&7&oAn empty scroll with infinite possibilities...", "", and "&6&lQUEST ITEM"
	set {collectorItems::goldenClock} to 1 of clock named "&6Golden Clock" with lore "&7&oLost among the many treasures", "&7&oof the basement.", "", and "&6&lQUEST ITEM"
	set {collectorItems::timeScroll} to 1 of flower banner pattern named "&b&lTime Scroll &7(Right Click)" with lore "&7&oLinks the past and present together...", "", and "&6&lQUEST ITEM"

	set {_goldenEggNBT} to nbt compound from "{SkullOwner:{Id:[I;-1083748053,1580942598,-2079554129,203661089],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYTkyYTFiMTExMzI0ZmYyZWMxYzE1MDMzMDQ4NjkwODE3NmVlYjM1YmM3NzU0MzgzN2UyZjMzYWNmN2QwMDdmMSJ9fX0=""}]}}}"
	set {collectorItems::collectorEgg} to 1 of player head with nbt {_goldenEggNBT} named "&e&kEGG &6&lGolden Egg &e&kEGG" with lore "&7&oSlightly burnt from the EggDonalds microwave...", "", and "&6&lQUEST ITEM"
	set {collectorItems::goldenEgg} to 1 of player head with nbt {_goldenEggNBT} named "&e&kEGG &6&lGolden Egg &e&kEGG" with lore "&7&oA very expensive egg...", "", and "&6&lQUEST ITEM"
	
	set {_hatNBT} to nbt compound from "{SkullOwner:{Id:[I;661374146,305286036,-1476850198,1087439663],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNmY4YmI3NDgzYzFlMzEyNjVjZmI5MTE3OGQ0Mzk5ODY5ZWEyMDllZDgwNjBmMTI2OTlhMTZlZTE4MWI0MmZiMCJ9fX0=""}]}}}"
	set {collectorItems::eggHat} to 1 of player head with nbt {_hatNBT} named "&eEgg Hat"

	send "&a[EggClicker] Reloaded collector items!" to console

command /getcollecteritems:
	permission: op 
	permission message: &cHey! No cheating ;)
	trigger:
		loop {collectorItems::*}:
			give player 1 of loop-value

on drop:
	player's gamemode is not creative
	{collectorItems::*} contains 1 of event-item
	cancel event
	send action bar "&7&oA mystical force prevents you from dropping the item..." to player

on right click on spore blossom:
	{player::%player's uuid%::collectorProgress} >= 2
	{player::%player's uuid%::suffix} is "fairy"
	chance of 1/10000:
		give player 1 of {collectorItems::magicDust}
		play sound "entity.vex.charge" at pitch 2 to player
		send "%nl%&7&oA bit of magic dust falls from the flower...%nl%" to player

on right click on bookshelf:
	{player::%player's uuid%::collectorProgress} >= 3
	{player::%player's uuid%::progression} is 1
	regions at player contains ("room-library" parsed as region)
	chance of 1/1: # 1/125000
		give player 1 of {collectorItems::emptyScroll}
		play sound "item.book.page_turn" to player
		send "%nl%&7&oUpon moving a book, an empty scroll falls out of the bookshelf...%nl%"

on right click:
	tool of player is {collectorItems::emptyScroll}
	cancel event

on right click on block of raw gold:
	{player::%player's uuid%::collectorProgress} >= 4
	difference between now and {player::%player's uuid%::lastPrestige} <= 1 hour
	regions at player contains ("room-basement" parsed as region)
	{player::%player's uuid%::eggs} >= {player::%player's uuid%::prestige} * 100000000000
	chance of 1/75000:
		give player 1 of {collectorItems::goldenClock}
		send "%nl%&7&oAt last! After long hours of searching, you spot the shine of a Golden Clock in the piles of treasure!%nl%" to player
		loop 4 times:
			play sound "block.bell.use" at pitch 0 to player
			wait 2 seconds

on right click:
	tool of player is {collectorItems::timeScroll}
	if "%player's world%" is "world":
		send "%nl%&7&oThe world shifts and EggClicker seems to downgrade several generations...%nl%" to player
		teleportPlayer(player, 466.5, 64, 657.5, 0, 90, "EggClickerOLD")
		play sound "entity.enderman.teleport" to player
	else:
		send "%nl%&7&oThe world shifts and you are pulled back to reality...%nl%" to player
		teleport player to {spawn}
		play sound "entity.enderman.teleport" to player

on right click on furnace:
	set {_uuid} to uuid of player
	if regions at event-block contains ("collectorEggFurnace" parsed as region):
		{player::%{_uuid}%::collectorProgress} is 6
		cancel event
		give player 1 of {collectorItems::collectorEgg}
		send "%nl%&7&oYou open the oven and find the Golden Egg inside...%nl%" to player
		set {player::%{_uuid}%::collectorProgress} to 7

on right click on smoker:
	set {_uuid} to uuid of player
	if regions at event-block contains ("goldenEggFurnace" parsed as region):
		{player::%{_uuid}%::collectorProgress} is 8
		cancel event
		give player 1 of {collectorItems::goldenEgg}
		broadcast "%nl%&a%player% &ehas found the &6&lGolden Egg&e!%nl%"
		send title "&6%player%" with subtitle "&ehas found the &6&lGolden Egg&e!" to all players for 10 seconds
		play sound "entity.ender_dragon.death" at volume 0.25 to all players
		set {player::%{_uuid}%::collectorProgress} to 9